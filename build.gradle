plugins {
    id "dev.kikugie.stonecutter"
    id "fabric-loom" version "1.6-SNAPSHOT"
    id 'maven-publish'
}

version =  property("mod.version") + "+" + property("deps.minecraft")
group = "com.mineblock11"

base {
    archivesBaseName = "mru"
}

repositories {
    mavenCentral()
    maven { url "https://maven.terraformersmc.com/releases" }
    maven { url "https://maven.isxander.dev/releases" }
    maven { url "https://jitpack.io" }
    maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
    maven { url "https://maven.quiltmc.org/repository/release" }
}

dependencies {
    minecraft("com.mojang:minecraft:${property("deps.minecraft")}")
    mappings("net.fabricmc:yarn:${property("deps.yarn")}:v2")
    modImplementation("net.fabricmc:fabric-loader:${property("deps.fabric_loader")}")

    modImplementation include("dev.isxander:yet-another-config-lib:${property("deps.yacl")}") {
        exclude group: "net.fabricmc.fabric-api", module: "fabric-api"
    }
}

processResources {
    filesMatching("fabric.mod.json") {
        expand 'mod_version': version,
                'target_minecraft': project.property('mod.target'),
                'target_yacl': project.property('deps.yacl'),
                'target_fabricloader': project.property('deps.fabric_loader')
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.release = 17
}

java {
    var version =  property("deps.minecraft").startsWith("1.20.6") ? JavaVersion.VERSION_21 : JavaVersion.VERSION_17
    sourceCompatibility = version
    targetCompatibility = version
    archivesBaseName = "mru"
    withSourcesJar()
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}" }
    }
}

// configure the maven publication
publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            groupId = "com.mineblock11"
            artifactId = "mru"
        }
    }

    repositories {
        maven {
            name = "mineblockMaven"
            url = "https://maven.imb11.dev/releases"
            credentials(PasswordCredentials)
        }
    }
}
